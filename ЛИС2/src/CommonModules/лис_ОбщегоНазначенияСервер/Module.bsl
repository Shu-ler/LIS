#Область ПрограммныйИнтерфейс

// Проверяет существование элементов с заданным набором значений реквизитов
//
// Параметры:
//  ЗаписываемыйЭлемент  - ссылка или объект - ссылка или сам записываемый объект
//  ПроверяемыеРеквизиты  - Структура - состав проверяемых реквизитов
//							* Ключ 		- Имя реквизита
//							* Значение  - Значение реквизита
// Возвращаемое значение:
//   Булево   - Истина - элемент уникальный (нет элементов с такими же значениями проверяемых реквизитов)
//
Функция ЭтоУникальныйЭлемент(ЗаписываемыйЭлемент, ПроверяемыеРеквизиты) Экспорт 
	Перем ИмяТаблицы, ТекстЗапроса, Запрос;
	
	Запрос = Новый Запрос;
	
	ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(ЗаписываемыйЭлемент);
	
	ТекстЗапроса = "ВЫБРАТЬ
	| Таблица.Ссылка
	|ИЗ
	|	"+ИмяТаблицы+" КАК Таблица
	|ГДЕ
	|";
	
	Разделитель = " ";
	
	// если элемент уже записан, нужно исключить его из выборки
	Если ЗначениеЗаполнено(ЗаписываемыйЭлемент.Ссылка) Тогда
		ТекстЗапроса = ТекстЗапроса + Разделитель + "Таблица.Ссылка <> &Ссылка
		|";
		Разделитель = " И ";
		Запрос.УстановитьПараметр("Ссылка", ЗаписываемыйЭлемент.Ссылка);
	КонецЕсли;
	
	
	Для каждого ТекЭлемент Из ПроверяемыеРеквизиты Цикл
		ТекстЗапроса = ТекстЗапроса + Разделитель + "Таблица." + ТекЭлемент.Ключ + " = &" + ТекЭлемент.Ключ;
		Разделитель = " И ";
	 	Запрос.УстановитьПараметр(ТекЭлемент.Ключ, ТекЭлемент.Значение);
	КонецЦикла; 

	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();
	
КонецФункции // ЭтоУникальныйЭлемент()

#КонецОбласти