Функция ПечатьЖурналОпределенияКомпонентовВПочве(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_лис_ЖурналОпределенияКомпонентовВПочве_ЖурналОпределенияКомпонентовВПочве";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.лис_ЖурналОпределенияКомпонентовВПочве.ПФ_MXL_ЖурналОпределенияКомпонентовВПочве");
		
	Для каждого ТекущийОбъект из МассивОбъектов Цикл
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Шапка
		Область = Макет.ПолучитьОбласть("Шапка");
		ТабличныйДокумент.Вывести(Область);
		
		НЗ = РегистрыСведений.лис_ЖурналОпределенияКомпонентовВПочве.СоздатьНаборЗаписей();
		НЗ.Отбор.Документ.Установить(ТекущийОбъект);
		НЗ.Прочитать();
		
		// Строки
		Для каждого Проба из НЗ Цикл
			
			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(Проба);
			Область.Параметры.Филиал = ТекущийОбъект.Организация; 
			ТабличныйДокумент.Вывести(Область);
			
		КонецЦикла;
		
	    // Подвал
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.Ответственный = ТекущийОбъект.Ответственный;
		ТабличныйДокумент.Вывести(Область);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции	

// Формирует печатные формы
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЖурналОпределенияКомпонентовВПочве") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ЖурналОпределенияКомпонентовВПочве", "Журнал определения компонентов в почве",
			ПечатьЖурналОпределенияКомпонентовВПочве(МассивОбъектов, ОбъектыПечати),, "Документ.лис_ЖурналОпределенияКомпонентовВПочве.ПФ_MXL_ЖурналОпределенияКомпонентовВПочве");
			
	КонецЕсли; 
	
КонецПроцедуры
