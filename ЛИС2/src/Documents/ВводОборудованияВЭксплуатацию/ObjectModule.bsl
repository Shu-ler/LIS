
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.лис_ИмуществоЛабораторий.Записывать = Истина;
	Движения.ЛабораторноеОборудованиеВЭксплуатации.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВводОборудованияВЭксплуатациюСписокОборудования.Оборудование,
	|	ВводОборудованияВЭксплуатациюСписокОборудования.ИнвентарныйНомер,
	|	ВводОборудованияВЭксплуатациюСписокОборудования.ЗаводскойНомер,
	|	ВводОборудованияВЭксплуатациюСписокОборудования.ЗаводскоеОбозначение,
	|	ВводОборудованияВЭксплуатациюСписокОборудования.НомерПаспортаНаОборудование,
	|	ВводОборудованияВЭксплуатациюСписокОборудования.ДатаИзготовления,
	|	ВводОборудованияВЭксплуатациюСписокОборудования.Оборудование.СрокЭксплуатации КАК СрокЭксплуатации,
	|	ВводОборудованияВЭксплуатациюСписокОборудования.РегистрационныйНомер
	|ИЗ
	|	Документ.ВводОборудованияВЭксплуатацию.СписокОборудования КАК ВводОборудованияВЭксплуатациюСписокОборудования
	|ГДЕ
	|	ВводОборудованияВЭксплуатациюСписокОборудования.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.лис_ИмуществоЛабораторий.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Лаборатория = Лаборатория;
		Движение.Объект = Выборка.Оборудование;
		Движение.ДатаИзготовления = Выборка.ДатаИзготовления;
		Движение.Количество = 1;
		
		Движение = Движения.ЛабораторноеОборудованиеВЭксплуатации.Добавить();
		Движение.Организация = Организация;
		Движение.Лаборатория = Лаборатория;
		Движение.ДатаВводаВЭксплуатацию = Дата;
		Движение.СрокДопустимойЭксплуатации = ДобавитьМесяц(Дата, Выборка.СрокЭксплуатации);
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	-лис_ИмуществоЛабораторийОстатки.КоличествоОстаток КАК Превышение,
	|	лис_ИмуществоЛабораторийОстатки.Лаборатория,
	|	лис_ИмуществоЛабораторийОстатки.Организация,
	|	лис_ИмуществоЛабораторийОстатки.Объект.Представление КАК Оборудование
	|ИЗ
	|	РегистрНакопления.лис_ИмуществоЛабораторий.Остатки(
	|			&МоментВремени,
	|			Организация = &Организация
	|				И Лаборатория = &Лаборатория) КАК лис_ИмуществоЛабораторийОстатки
	|ГДЕ
	|	лис_ИмуществоЛабораторийОстатки.КоличествоОстаток < 0";
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Лаборатория", Лаборатория);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Недостаточно оборудования " + Выборка.Оборудование + " в лаборатории " + Выборка.Лаборатория + " (организация: " + Выборка.Организация +
						") в количестве " + Выборка.Превышение; 
		Сообщение.Сообщить();
		Отказ = Истина;		
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	
КонецПроцедуры
