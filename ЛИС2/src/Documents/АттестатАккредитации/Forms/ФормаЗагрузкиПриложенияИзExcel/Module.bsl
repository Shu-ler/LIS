////////////////////////////////////////////////////////////////////////////////
// МЕТОДВ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ЗагрузитьЛисты(АдресФайла)
	ИмяФайлаНаСервере = КаталогВременныхФайлов() + "загрузка из excel (" + Формат(ТекущаяДата(), "ДФ=""гггг.ММ.дд ЧЧ-мм-сс""") + ").xls";
	Файл = ПолучитьИзВременногоХранилища(АдресФайла);
	Файл.Записать(ИмяФайлаНаСервере);
	
	Попытка
		Excel = Новый COMObject("Excel.Application");
	Исключение
		Сообщить("Ошибка при загрузке Microsoft Excel." + Символы.ПС + ОписаниеОшибки(), СтатусСообщения.Внимание);
		Возврат;
	КонецПопытки;
	
	Попытка
		ExcelФайл = Excel.WorkBooks.Open(ИмяФайлаНаСервере);
	Исключение
		Сообщить("Невозможно открыть файл " + ИмяФайлаНаСервере + " !!!" + Символы.ПС + ОписаниеОшибки(), СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
		
	Элементы.Лист.СписокВыбора.Очистить();
	КоличествоЛистов = ExcelФайл.Sheets.Count;
	Для НомерЛиста = 1 по КоличествоЛистов Цикл
		Элементы.Лист.СписокВыбора.Добавить(Строка(ExcelФайл.Sheets(НомерЛиста).Name));
	КонецЦикла;
	
	Если Элементы.Лист.СписокВыбора.Количество() > 0 Тогда
		Лист = Элементы.Лист.СписокВыбора[0].Значение;
	Иначе
		Лист = "";
	КонецЕсли;
	
	Excel.Application.DisplayAlerts = Ложь;
	Excel.Application.Quit();
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗагрузкуНаСервере()
	Попытка
		Excel = Новый COMObject("Excel.Application");		
	Исключение
		Сообщить("Ошибка при загрузке Microsoft Excel." + Символы.ПС + ОписаниеОшибки(), СтатусСообщения.Внимание);
		Возврат;
	КонецПопытки;
	
	Попытка
		ExcelФайл = Excel.WorkBooks.Open(ИмяФайлаНаСервере);	
	Исключение
		Сообщить("Невозможно открыть файл " + ИмяФайлаНаСервере + " !!!" + Символы.ПС + ОписаниеОшибки(), СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
	
	ТаблицаПриложения.Очистить();
	
	ExcelФайл.Sheets(Лист).Select();
	
	СтолбецНомер = рСтолбец;
	СтрокаНомер = рСтрока;
	РазделУчета = Excel.Cells(СтрокаНомер, СтолбецНомер).Value;
	Если Не ЗначениеЗаполнено(РазделУчета) Тогда
		Сообщить("Не найдено кадастровых номеров. Проверьте лист, строку и столбец, с которых начинается загрузка");
	Иначе
		Пока ЗначениеЗаполнено(РазделУчета) Цикл
			НоваяСтрока = ТаблицаПриложения.Добавить();
			
			НоваяСтрока.РазделУчета 			= ЗначениеПеречисленияПоСинониму(РазделУчета, Перечисления.РазделыУчетаЭкоаналитическогоКонтроля);
			НоваяСтрока.ЗагрязняющееВещество 	= НайтиЗВ(Excel.Cells(СтрокаНомер, СтолбецНомер+1).Value, НоваяСтрока.РазделУчета);
			НоваяСтрока.ДиапазонОт 				= Число(Excel.Cells(СтрокаНомер, СтолбецНомер+2).Value);
			НоваяСтрока.ДиапазонДо 				= Число(Excel.Cells(СтрокаНомер, СтолбецНомер+3).Value);
			НоваяСтрока.ЕдиницаИзмерения 		= Справочники.ОКЕИ.НайтиПоНаименованию(Excel.Cells(СтрокаНомер, СтолбецНомер+4).Value);
			НоваяСтрока.Методика 				= Справочники.МетодикиОпределенияИзмеренияАнализа.НайтиПоНаименованию(Excel.Cells(СтрокаНомер, СтолбецНомер+5).Value);
			
			СтрокаНомер = СтрокаНомер + 1;
			РазделУчета = Excel.Cells(СтрокаНомер, СтолбецНомер).Value;
		КонецЦикла;
	КонецЕсли; 
	
	Excel.Application.DisplayAlerts = Ложь;
	Excel.Application.Quit();
КонецПроцедуры

Функция ЗначениеПеречисленияПоСинониму(Синоним, ПеречислениеМенеджер)
	
	ПолученноеЗначение = ПеречислениеМенеджер.ПустаяСсылка(); 
	ЗначенияПеречисления = ПолученноеЗначение.Метаданные().ЗначенияПеречисления; 
	Значение = ЗначенияПеречисления.Найти(СтрЗаменить(Синоним, " ", "")); 
	Если Значение <> Неопределено Тогда 
		ПолученноеЗначение = ПеречислениеМенеджер[ЗначенияПеречисления.Индекс(Значение)]; 
	КонецЕсли;
	
	Возврат ПолученноеЗначение;
	
КонецФункции

Функция НайтиЗВ(Наименование, РазделУчета)
	
	Если РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.АтмосферныйВоздух")
		ИЛИ РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.ПромышленныеВыбросы") Тогда
		
		НайденнйЗВ = Справочники.ЗВАтмосфера.НайтиПоНаименованию(Наименование);
		Если ЗначениеЗаполнено(НайденнйЗВ) Тогда
			Возврат НайденнйЗВ;
		КонецЕсли;
		
	ИначеЕсли РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.СточныеВоды")
		ИЛИ РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.ПриродныеВоды") Тогда
		
		НайденнйЗВ = Справочники.ЗВВода.НайтиПоНаименованию(Наименование);
		Если ЗначениеЗаполнено(НайденнйЗВ) Тогда
			Возврат НайденнйЗВ;
		КонецЕсли;
		
		НайденнйЗВ = Справочники.КонтролируемыеПоказателиВВоде.НайтиПоНаименованию(Наименование);
		Если ЗначениеЗаполнено(НайденнйЗВ) Тогда
			Возврат НайденнйЗВ;
		КонецЕсли;
		
	ИначеЕсли РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.Почвы")
		ИЛИ РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.Почвы") Тогда
		
		НайденнйЗВ = Справочники.КонтролируемыеПараметрыВПочвеДонныхОтложениях.НайтиПоНаименованию(Наименование);
		Если ЗначениеЗаполнено(НайденнйЗВ) Тогда
			Возврат НайденнйЗВ;
		КонецЕсли;
		
	ИначеЕсли РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.ФизическиеФакторы") Тогда
		
		НайденнйЗВ = Справочники.ФакторыФизическогоВоздействия.НайтиПоНаименованию(Наименование);
		Если ЗначениеЗаполнено(НайденнйЗВ) Тогда
			Возврат НайденнйЗВ;
		КонецЕсли;
		
	ИначеЕсли РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.АтмосферныеОсадки") Тогда
		
		НайденнйЗВ = Справочники.ВеществаИПараметрыВАтмосферныхОсадках.НайтиПоНаименованию(Наименование);
		Если ЗначениеЗаполнено(НайденнйЗВ) Тогда
			Возврат НайденнйЗВ;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	рСтрока = 1;
	рСтолбец = 1;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ПеренестиВФорму(Команда)
	
	Для Каждого Строка Из ТаблицаПриложения Цикл
		НоваяСтрока = ВладелецФормы.Объект.Приложение.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	ВладелецФормы.Модифицированность = Истина;
	ЭтаФорма.Закрыть();
КонецПроцедуры



&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ИмяФайла = "";
	АдресФайла = Неопределено;
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗавершениеВыбораФайла", ЭтотОбъект), АдресФайла, ИмяФайла, , УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораФайла(Результат, АдресФайла, ИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		ЭтаФорма.ИмяФайла = ИмяФайла;
		ЗагрузитьЛисты(АдресФайла);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
		ПоказатьПредупреждение(, "Не выбран файл!");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Лист) Тогда
		ПоказатьПредупреждение(, "Не выбран лист в файле!");
		Возврат;
	КонецЕсли;
	
	ВыполнитьЗагрузкуНаСервере();
	ПоказатьПредупреждение(, "Загрузка завершена!");
КонецПроцедуры
