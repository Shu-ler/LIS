#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Основное.Отбор.Элементы.Очистить(); 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "Документ", Объект.Ссылка, ВидСравненияКомпоновкиДанных.Равно,,Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Основное.Отбор.Элементы.Очистить(); 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "Документ", Объект.Ссылка, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	
КонецПроцедуры


&НаКлиенте
Процедура ОсновноеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = новый Структура;
	ПараметрыФормы.Вставить("Документ", Объект.Ссылка);
	ПараметрыФормы.Вставить("АктОтбора", Элементы.Основное.ТекущиеДанные.АктОтбора);
	
	ОткрытьФорму("РегистрСведений.лис_ЖурналОпределенияКомпонентовВАтмосферномВоздухе.Форма.ФормаЗаписейПоАкту", ПараметрыФормы, ЭтаФорма, , ВариантОткрытияОкна.ОтдельноеОкно);

КонецПроцедуры

&НаКлиенте
Процедура ОсновноеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.лис_АктОтбораПроб") Тогда
		ОсновноеОбработкаВыбораНаСервере(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОсновноеОбработкаВыбораНаСервере(АктОтбора)
	
	Объект.Организация = АктОтбора.Организация;
	Объект.Лаборатория = АктОтбора.Лаборатория;

	НЗ = РегистрыСведений.лис_ЖурналОпределенияКомпонентовВАтмосферномВоздухе.СоздатьНаборЗаписей();
	НЗ.Отбор.Документ.Установить(Объект.Ссылка);
	НЗ.Прочитать();
	
	Для Каждого Строка Из АктОтбора.Пробы Цикл
		
		НоваяСтрока = НЗ.Добавить();
		НоваяСтрока.Документ = Объект.Ссылка;
		НоваяСтрока.АктОтбора = АктОтбора;
		НоваяСтрока.ТочкаОтбораПробы = Строка.ТочкаОтбораПробы;
		НоваяСтрока.ОпределяемыйПоказатель = Строка.ОпределяемыйПоказатель;
		НоваяСтрока.ШифрПробы = Строка.ШифрПробы;
		НоваяСтрока.ОбъемПробы = Строка.ОбъемПробы;
		НоваяСтрока.ЕдиницаИзмерения = Строка.ЕдиницаИзмерения;
		НоваяСтрока.ДатаОтбора = АктОтбора.ДатаОтбора;
		
	КонецЦикла;
	
	НЗ.Записать(Истина);
	
	Элементы.Основное.Обновить();
	
КонецПроцедуры


&НаКлиенте
Процедура ДобавитьИзАкта(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭтотОбъект.Записать(); 
		Основное.Отбор.Элементы.Очистить(); 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "Документ", Объект.Ссылка, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	КонецЕсли;
	
	РазделУчета = Новый Массив;
	РазделУчета.Добавить(ПредопределенноеЗначение("Перечисление.РазделыУчетаЭкоаналитическогоКонтроля.АтмосферныйВоздух"));
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("РазделУчета", РазделУчета);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("Документ.лис_АктОтбораПроб.ФормаВыбора", ПараметрыФормы, Элементы.Основное);

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// ОкончаниеПериода
	Если ЗначениеЗаполнено(Объект.ОкончаниеПериода)  Тогда
		Если (Объект.ОкончаниеПериода < Объект.НачалоПериода) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата окончания периода должна быть больше даты начала периода'"),,"ОкончаниеПериода","Объект",Отказ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти