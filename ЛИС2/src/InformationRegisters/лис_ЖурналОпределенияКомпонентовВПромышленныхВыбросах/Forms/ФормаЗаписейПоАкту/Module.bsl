#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыпуститьПротоколНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПротоколАнализаВыбросов.Ссылка
	               |ИЗ
	               |	Документ.ПротоколАнализаВыбросов КАК ПротоколАнализаВыбросов
	               |ГДЕ
	               |	ПротоколАнализаВыбросов.лис_АктОтбора = &Акт
	               |	И ПротоколАнализаВыбросов.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Акт", АктОтбора);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		
		Протокол = Документы.ПротоколАнализаВыбросов.СоздатьДокумент();
		Протокол.Дата = ТекущаяДата();
		Протокол.лис_АктОтбора = АктОтбора;
		Протокол.Организация = Документ.Организация;
		Протокол.ПроизводственнаяПлощадка = АктОтбора.ОбъектКонтроля;
		Протокол.ДатаПротокола = ТекущаяДата();
		Протокол.ДатаОтбораНачало = Документ.НачалоПериода;
		Протокол.ДатаОтбораОкончание = Документ.ОкончаниеПериода;
		Протокол.ЦельОтбораПроб = АктОтбора.ЦельОтбораПроб;
		Протокол.ДатаОтбораПроб = АктОтбора.ДатаОтбора;
		Протокол.Лаборатория = АктОтбора.Лаборатория;
		
		НаборЗаписей = РегистрыСведений.лис_ЖурналОпределенияКомпонентовВПромышленныхВыбросах.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.АктОтбора.Установить(АктОтбора);
		НаборЗаписей.Отбор.Документ.Установить(Документ);
		НаборЗаписей.Прочитать();
		
		Для Каждого Строка Из НаборЗаписей Цикл
			// Galaktionovva; 2017-11-20: ТЧ "ПереченьВеществВыбрасываемыхОтОбъекта" заполняется как дочерняя от ТЧ 
			// "ПереченьИсточниковЗагрязнения" данных по которой нет.
			//НоваяСтрока = Протокол.ПереченьВеществВыбрасываемыхОтОбъекта.Добавить();
			//НоваяСтрока.ЗагрязняющееВещество = Строка.ОпределяемыйПоказатель;
			//НоваяСтрока.ИдентификаторИЗА = ;
			
			Протокол.МестоОтбораПроб = Строка.ТочкаОтбораПробы;
			Протокол.МетодикаВыполненияИзмерений = Строка.Методика;
		КонецЦикла;
		
		Протокол.Записать();
		
		Возврат Протокол.Ссылка;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	АктОтбора = Параметры.АктОтбора;

	Основное.Отбор.Элементы.Очистить(); 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "Документ", Документ, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "АктОтбора", АктОтбора, ВидСравненияКомпоновкиДанных.Равно,,Истина);

КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыпуститьПротокол(Команда)
	
	Протокол = ВыпуститьПротоколНаСервере();
	
	ПараметрыФормы = Новый Структура("Ключ", Протокол);
	
	ОткрытьФорму("Документ.ПротоколАнализаВыбросов.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

#КонецОбласти
