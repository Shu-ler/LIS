#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыпуститьПротоколНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПротоколАнализаПочвДонныхОтложений.Ссылка
	               |ИЗ
	               |	Документ.ПротоколАнализаПочвДонныхОтложений КАК ПротоколАнализаПочвДонныхОтложений
	               |ГДЕ
	               |	ПротоколАнализаПочвДонныхОтложений.лис_АктОтбора = &Акт
	               |	И ПротоколАнализаПочвДонныхОтложений.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Акт", АктОтбора);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		
		Протокол = Документы.ПротоколАнализаПочвДонныхОтложений.СоздатьДокумент();
		Протокол.Дата = ТекущаяДата();
		Протокол.лис_АктОтбора = АктОтбора;
		Протокол.Организация = Документ.Организация;
		Если АктОтбора.РазделУчета = Перечисления.РазделыУчетаЭкоаналитическогоКонтроля.Почвы Тогда
			Протокол.ВидПротокола = Перечисления.ВидыПротоколовПочва.Почва;
		ИначеЕсли АктОтбора.РазделУчета = Перечисления.РазделыУчетаЭкоаналитическогоКонтроля.ДонныеОтложения Тогда
			Протокол.ВидПротокола = Перечисления.ВидыПротоколовПочва.ДонныеОтложения;
		КонецЕсли;
		Протокол.ДатаПротокола = ТекущаяДата();
		
		НаборЗаписей = РегистрыСведений.лис_ЖурналОпределенияКомпонентовВПочве.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.АктОтбора.Установить(АктОтбора);
		НаборЗаписей.Отбор.Документ.Установить(Документ);
		НаборЗаписей.Прочитать();
		
		Для Каждого Строка Из НаборЗаписей Цикл
			НоваяСтрока = Протокол.ТаблицаЗВ.Добавить();
			НоваяСтрока.ДатаОтбора = АктОтбора.ДатаОтбора;
			НоваяСтрока.ТочкаКонтроля = Строка.ТочкаОтбораПробы;
			НоваяСтрока.ОбъектКонтроля = АктОтбора.ОбъектКонтроля;
			НоваяСтрока.НомерАкта = АктОтбора.Номер;
			НоваяСтрока.ДатаИспытания = Строка.ДатаАнализа;
			НоваяСтрока.НомерОбразца = Строка.ШифрПробы;
			НоваяСтрока.ЗагрязняющееВещество = Строка.ОпределяемыйПоказатель;
			НоваяСтрока.МассоваяДоля = Строка.СодержаниеПоРасчету;
			НоваяСтрока.ЕдиницаИзмерения = Строка.ЕдиницаИзмерения;
			НоваяСтрока.ОбъектОтбораПроб = АктОтбора.ОбъектОтбораПроб;
		КонецЦикла;
		
		Протокол.Записать();
		
		Возврат Протокол.Ссылка;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	АктОтбора = Параметры.АктОтбора;

	Основное.Отбор.Элементы.Очистить(); 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "Документ", Документ, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "АктОтбора", АктОтбора, ВидСравненияКомпоновкиДанных.Равно,,Истина);

КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыпуститьПротокол(Команда)
	
	Протокол = ВыпуститьПротоколНаСервере();
	
	ПараметрыФормы = Новый Структура("Ключ", Протокол);
	
	ОткрытьФорму("Документ.ПротоколАнализаПочвДонныхОтложений.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

#КонецОбласти
