#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыпуститьПротоколНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПротоколИзмеренияФизическихФакторов.Ссылка
	               |ИЗ
	               |	Документ.ПротоколИзмеренияФизическихФакторов КАК ПротоколИзмеренияФизическихФакторов
	               |ГДЕ
	               |	ПротоколИзмеренияФизическихФакторов.лис_АктОтбора = &Акт
	               |	И ПротоколИзмеренияФизическихФакторов.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Акт", АктОтбора);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		
		Протокол = Документы.ПротоколИзмеренияФизическихФакторов.СоздатьДокумент();
		Протокол.Дата = ТекущаяДата();
		Протокол.лис_АктОтбора = АктОтбора;
		Протокол.Организация = Документ.Организация;
		Протокол.ДатаПротокола = ТекущаяДата();
		
		НаборЗаписей = РегистрыСведений.лис_ЖурналОпределенияУровняШума.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.АктОтбора.Установить(АктОтбора);
		НаборЗаписей.Отбор.Документ.Установить(Документ);
		НаборЗаписей.Прочитать();
		
		Для Каждого Строка Из НаборЗаписей Цикл
			НоваяСтрока = Протокол.ФизическиеФакторы.Добавить();
			НоваяСтрока.ДатаИзмерения = Строка.ДатаОтбора;
			НоваяСтрока.ТочкаКонтроля = Строка.ТочкаОтбораПробы;
			НоваяСтрока.ЕдиницаИзмерения = Строка.ЕдиницаИзмерения;
			НоваяСтрока.УровеньЭквивалент = Строка.СреднийУровеньЗвукаЭквивалентный;
			НоваяСтрока.УровеньМакс = Строка.СреднийУровеньЗвукаМаксимальный;
			НоваяСтрока.ФакторВоздействия = Строка.ОпределяемыйПоказатель;
		КонецЦикла;
		
		Протокол.Записать();
		
		Возврат Протокол.Ссылка;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	АктОтбора = Параметры.АктОтбора;

	Основное.Отбор.Элементы.Очистить(); 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "Документ", Документ, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "АктОтбора", АктОтбора, ВидСравненияКомпоновкиДанных.Равно,,Истина);

КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыпуститьПротокол(Команда)
	
	Протокол = ВыпуститьПротоколНаСервере();
	
	ПараметрыФормы = Новый Структура("Ключ", Протокол);
	
	ОткрытьФорму("Документ.ПротоколИзмеренияФизическихФакторов.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

#КонецОбласти
