#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыпуститьПротоколНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПротоколАнализаСточныхВод.Ссылка
	               |ИЗ
	               |	Документ.ПротоколАнализаСточныхВод КАК ПротоколАнализаСточныхВод
	               |ГДЕ
	               |	ПротоколАнализаСточныхВод.лис_АктОтбора = &Акт
	               |	И ПротоколАнализаСточныхВод.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Акт", АктОтбора);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		
		Протокол = Документы.ПротоколАнализаСточныхВод.СоздатьДокумент();
		Протокол.Дата = ТекущаяДата();
		Протокол.лис_АктОтбора = АктОтбора;
		Протокол.Организация = Документ.Организация;
		Протокол.ОбъектКонтроля = АктОтбора.ОбъектКонтроля;
		Протокол.ОбъектОтбораПроб = АктОтбора.ОбъектОтбораПроб;
		Протокол.ДатаПротокола = ТекущаяДата();
		Протокол.НомерПробы	= АктОтбора.РегистрационныйНомерПробы;
		Протокол.ДатаСдачиПробВЛабораторию = АктОтбора.ВремяИДатаДоставкиВЛабораторию;
		
		НаборЗаписей = РегистрыСведений.лис_ЖурналОпределенияКомпонентовВВоде.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.АктОтбора.Установить(АктОтбора);
		НаборЗаписей.Отбор.Документ.Установить(Документ);
		НаборЗаписей.Прочитать();
		
		Для Каждого Строка Из НаборЗаписей Цикл
			НоваяСтрока = Протокол.ТаблицаЗВ.Добавить();
			НоваяСтрока.ЗагрязняющееВещество = Строка.ОпределяемыйПоказатель;
			НоваяСтрока.Концентрация = Строка.СодержаниеПоРасчету;
			НоваяСтрока.ЕдиницаИзмерения = Строка.ЕдиницаИзмерения;
			
			Протокол.МестоОтбора = Строка.ТочкаОтбораПробы;
		КонецЦикла;
		
		Протокол.Записать();
		
		Возврат Протокол.Ссылка;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Документ = Параметры.Документ;
	АктОтбора = Параметры.АктОтбора;

	Основное.Отбор.Элементы.Очистить(); 
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "Документ", Документ, ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Основное, "АктОтбора", АктОтбора, ВидСравненияКомпоновкиДанных.Равно,,Истина);

КонецПроцедуры


&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыпуститьПротокол(Команда)
	
	Протокол = ВыпуститьПротоколНаСервере();
	
	ПараметрыФормы = Новый Структура("Ключ", Протокол);
	
	ОткрытьФорму("Документ.ПротоколАнализаСточныхВод.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

#КонецОбласти
