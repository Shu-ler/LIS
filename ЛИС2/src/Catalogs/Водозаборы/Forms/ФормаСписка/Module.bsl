
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ИерархияОрганизацииПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборПоОрганизации(Элементы.ИерархияОрганизации.ТекущаяСтрока);

КонецПроцедуры // Процедура ИерархияОрганизацииПриАктивизацииСтроки(Элемент)

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ОбщегоНазначения.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

КонецПроцедуры // Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		
		ПараметрыФормыВодозаборы = Новый Структура("ЗначениеКопирования", Элемент.ТекущаяСтрока);
		
	Иначе
		
		ДанныеЗаполнения = Новый Структура;
				
		Если Элементы.ИерархияОрганизации.ТекущаяСтрока <> Неопределено Тогда
			ДанныеЗаполнения.Вставить("Организация", Элементы.ИерархияОрганизации.ТекущаяСтрока);
		КонецЕсли; 
		
		ПараметрыФормыВодозаборы = Новый Структура("ЗначенияЗаполнения", ДанныеЗаполнения);
		
	КонецЕсли; // Если Копирование Тогда 
	
	ОткрытьФорму("Справочник.Водозаборы.ФормаОбъекта", ПараметрыФормыВодозаборы, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

&НаКлиенте
Процедура ИерархияОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УстановитьОтборПоОрганизации(ВыбраннаяСтрока);
	
КонецПроцедуры // Процедура ИерархияОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура УстановитьОтборПоОрганизации(ГруппаОрганизации)
	
	ИзменитьЭлементОтбораСписка(Список, "Организация",
		ГруппаОрганизации, ЗначениеЗаполнено(ГруппаОрганизации), ВидСравненияКомпоновкиДанных.ВИерархии);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭлементОтбораСписка(СписокДляОтбора, ИмяПоля, ПравоеЗначение = Неопределено, Использование = Ложь, ВидСравнения = Неопределено) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		СписокДляОтбора.КомпоновщикНастроек.Настройки.Отбор, 
		ИмяПоля);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокДляОтбора.КомпоновщикНастроек.Настройки.Отбор, 
		ИмяПоля, 
		ПравоеЗначение, 
		ВидСравнения, 
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ, 
		Использование); 
		
КонецПроцедуры 

#КонецОбласти



