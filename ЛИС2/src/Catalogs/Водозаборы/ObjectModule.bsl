#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;		

	// Работа с бизнес-процессами
	Если Обработки.СозданиеНСИ.ИспользоватьМеханизмБизнесПроцессов( Ссылка ) Тогда
		Обработки.СозданиеНСИ.ЗапуститьБизнесПроцессСогласования( Ссылка );
	КонецЕсли;

КонецПроцедуры

Процедура ПередУдалением(Отказ)

// 1СКСУ - Казанцев И.В. 19.01.17 11:15
// При провере АПК не обнаружена конструкция "Если ОбменДанными.Загрузка Тогда ...".
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 
// 1СКСУ - Казанцев И.В. 19.01.17 11:15 Конец

	
	// Работа с бизнес-процессами
	Если Обработки.СозданиеНСИ.ИспользоватьМеханизмБизнесПроцессов( Ссылка ) Тогда
		Если Не Обработки.СозданиеНСИ.ЭлементДоступенДляРедактирования( Ссылка ) Тогда
			Отказ = Истина;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	СписокВидовВодныхОбъектовУстье = Справочники.Водозаборы.ПолучитьСписокВидовВодныхОбъектовРасстояниеДоУстья();
	
	Если СписокВидовВодныхОбъектовУстье.Найти(КодТипаИсточникаПриемника) <> Неопределено Тогда 
		ПроверяемыеРеквизиты.Добавить("РасстояниеОтУстья");
	КонецЕсли; // Если СписокВидовВодныхОбъектовУстье.Найти(КодТипаИсточникаВодоснабжения) <> Неопределено Тогда
	
	Если КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.ЛивневойКоллектор") Тогда 
		ПроверяемыеРеквизиты.Добавить("ВодныйОбъект");		
	КонецЕсли; // Если КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.ЛивневойКоллектор") Тогда 
	
	Если (КатегорияКачестваВоды = ПредопределенноеЗначение("Справочник.КатегорииКачестваВоды.Минеральная") Или 
		КатегорияКачестваВоды = ПредопределенноеЗначение("Справочник.КатегорииКачестваВоды.Термальная")) И 
		(КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.ПодземныйВодныйОбъект") И 
		КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.ШахтаРудникНефтепромыселКарьер")) Тогда 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для выбранной категории качества воды код типа источника должен быть равен 60 или 61", 
		ЭтотОбъект,"КодТипаИсточникаПриемника",, Отказ);
		
	ИначеЕсли КатегорияКачестваВоды = ПредопределенноеЗначение("Справочник.КатегорииКачестваВоды.Ливневая") И 
		КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.ЛивневойКоллектор") Тогда 	
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для выбранной категории качества воды код типа источника должен быть равен 99", 
		ЭтотОбъект,"КодТипаИсточникаПриемника",, Отказ);
		
	ИначеЕсли (КатегорияКачестваВоды = ПредопределенноеЗначение("Справочник.КатегорииКачестваВоды.ШахтноРудничная") Или
		КатегорияКачестваВоды = ПредопределенноеЗначение("Справочник.КатегорииКачестваВоды.Карьерная")) И 
		КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.ШахтаРудникНефтепромыселКарьер") Тогда 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для выбранной категории качества воды код типа источника должен быть равен 61", 
		ЭтотОбъект,"КодТипаИсточникаПриемника",, Отказ);		
		
	ИначеЕсли КатегорияКачестваВоды = ПредопределенноеЗначение("Справочник.КатегорииКачестваВоды.КоллекторноДренажная") И
		КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.СкважинаВертикальнаяУровняГрунтовыхВод") И 
		КодТипаИсточникаПриемника <> ПредопределенноеЗначение("Справочник.КодыТипыИсточниковПриемников.КоллекторОросительнойСистемы") Тогда 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для выбранной категории качества воды код типа источника должен быть равен 62 или 71", 
		ЭтотОбъект,"КодТипаИсточникаПриемника",, Отказ);
		
	КонецЕсли; // Если (КатегорияКачестваВоды = ПредопределенноеЗначение("Справочник.КатегорииКачестваВоды.Минеральная") Или  
		
КонецПроцедуры

#КонецОбласти
