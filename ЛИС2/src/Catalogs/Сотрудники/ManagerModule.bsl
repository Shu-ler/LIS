
Функция СформироватьТабДокСведенийОСотрудниках(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Справочники.Сотрудники.ПолучитьМакет("СведенияОРаботникахЛаборатории");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Сотрудники.Фамилия + "" "" + Сотрудники.Имя + "" "" + Сотрудники.Отчество КАК ФИО,
	|	Сотрудники.СНИЛС,
	|	Сотрудники.ДатаРождения,
	|	Сотрудники.МестоРождения,
	|	Сотрудники.ОснованиеДляПривлеченияТруда,
	|	ВЫБОР
	|		КОГДА Сотрудники.ВидТрудоустройства = ЗНАЧЕНИЕ(Перечисление.ВидыТрудоустройства.ОсновноеМестоРаботы)
	|			ТОГДА ""по основному месту работы""
	|		КОГДА Сотрудники.ВидТрудоустройства = ЗНАЧЕНИЕ(Перечисление.ВидыТрудоустройства.Совместительство)
	|			ТОГДА ""по совместительству""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидТрудоустройства,
	|	Сотрудники.Должность,
	|	Сотрудники.Ссылка КАК Ссылка,
	|	Сотрудники.Примечание,
	|	Сотрудники.КоличествоЛетОпыта
	|ПОМЕСТИТЬ ВТ_ОбщиеСведения
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиВыполняемыеФункции.ВыполняемаяФункция,
	|	ВТ_ОбщиеСведения.Ссылка КАК Ссылка
	|ИЗ
	|	ВТ_ОбщиеСведения КАК ВТ_ОбщиеСведения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники.ВыполняемыеФункции КАК СотрудникиВыполняемыеФункции
	|		ПО ВТ_ОбщиеСведения.Ссылка = СотрудникиВыполняемыеФункции.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ОбщиеСведения.ФИО
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОбразование.НаименованиеУчебногоЗаведения,
	|	СотрудникиОбразование.ДатаОкончания,
	|	СотрудникиОбразование.КвалификацияПоДокументу,
	|	СотрудникиОбразование.РеквизитыДокументаОбОбразовании,
	|	ВТ_ОбщиеСведения.Ссылка КАК Ссылка
	|ИЗ
	|	ВТ_ОбщиеСведения КАК ВТ_ОбщиеСведения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники.Образование КАК СотрудникиОбразование
	|		ПО ВТ_ОбщиеСведения.Ссылка = СотрудникиОбразование.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ОбщиеСведения.ФИО
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиПрактическийОпыт.ДатаНачала,
	|	СотрудникиПрактическийОпыт.ДатаОкончания,
	|	СотрудникиПрактическийОпыт.Организация,
	|	ВТ_ОбщиеСведения.Ссылка КАК Ссылка,
	|	СотрудникиПрактическийОпыт.ВидДеятельности.Представление КАК ВидДеятельности
	|ИЗ
	|	ВТ_ОбщиеСведения КАК ВТ_ОбщиеСведения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники.ПрактическийОпыт КАК СотрудникиПрактическийОпыт
	|		ПО ВТ_ОбщиеСведения.Ссылка = СотрудникиПрактическийОпыт.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ОбщиеСведения.ФИО
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОбщиеСведения.ФИО КАК ФИО,
	|	ВТ_ОбщиеСведения.СНИЛС,
	|	ВТ_ОбщиеСведения.ДатаРождения,
	|	ВТ_ОбщиеСведения.МестоРождения,
	|	ВТ_ОбщиеСведения.ОснованиеДляПривлеченияТруда,
	|	ВТ_ОбщиеСведения.ВидТрудоустройства,
	|	ВТ_ОбщиеСведения.Должность,
	|	ВТ_ОбщиеСведения.Ссылка,
	|	ВТ_ОбщиеСведения.Примечание,
	|	ВТ_ОбщиеСведения.КоличествоЛетОпыта
	|ИЗ
	|	ВТ_ОбщиеСведения КАК ВТ_ОбщиеСведения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФИО";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаОбщая = МассивРезультатов[4].Выбрать();
	ВыборкаФункции = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбразование = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОпыт = МассивРезультатов[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Номер = 0;
	Пока ВыборкаОбщая.Следующий() Цикл
		
		Номер = Номер + 1;
		ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ОбластьСтрока.Параметры.Заполнить(ВыборкаОбщая);
		ОбластьСтрока.Параметры.Номер = Номер;
		ОбластьСтрока.Параметры.ДатаРождения = Формат(ВыборкаОбщая.ДатаРождения, "ДФ=dd.MM.yyyy");
		
		Если ВыборкаФункции.Следующий() Тогда
			ВыборкаДетальныеЗаписи = ВыборкаФункции.Выбрать();
			Если ВыборкаДетальныеЗаписи.Количество() Тогда
				СтрокаФункций = "";
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					
					СтрокаФункций = СтрокаФункций + Символы.ПС + "-" + ВыборкаДетальныеЗаписи.ВыполняемаяФункция + ";";
					
				КонецЦикла;
				ОбластьСтрока.Параметры.Функции = СтрокаФункций;
			КонецЕсли;
		КонецЕсли;
		
		Если ВыборкаОбразование.Следующий() Тогда
			ВыборкаДетальныеЗаписи = ВыборкаОбразование.Выбрать();
			Если ВыборкаДетальныеЗаписи.Количество() Тогда
				СтрокаОбразования = "";
				Индекс = 0;
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Индекс = Индекс + 1;
					СтрокаОбразования = СтрокаОбразования + ВыборкаДетальныеЗаписи.НаименованиеУчебногоЗаведения + 
										", " + Формат(ВыборкаДетальныеЗаписи.ДатаОкончания, "ДФ=гггг") + ", " + 
										"«" + ВыборкаДетальныеЗаписи.КвалификацияПоДокументу + "»" + ", " +
										ВыборкаДетальныеЗаписи.РеквизитыДокументаОбОбразовании;
										
					Если Индекс <> ВыборкаДетальныеЗаписи.Количество() Тогда
						СтрокаОбразования = СтрокаОбразования + Символы.ПС + Символы.ПС;
					КонецЕсли;			
				КонецЦикла;
				ОбластьСтрока.Параметры.Образование = СтрокаОбразования;
			КонецЕсли;
		КонецЕсли;
		
		Если ВыборкаОпыт.Следующий() Тогда
			ВыборкаДетальныеЗаписи = ВыборкаОпыт.Выбрать();
			Если ВыборкаДетальныеЗаписи.Количество() Тогда
				СтрокаОпыта = "";
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					СтрокаОпыта = СтрокаОпыта + Символы.ПС + ВыборкаДетальныеЗаписи.Организация + ":" + Символы.ПС + Формат(ВыборкаДетальныеЗаписи.ДатаНачала, "ДФ=dd.MM.yyyy");
					Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаОкончания) Тогда
						СтрокаОпыта = СтрокаОпыта + " – " + Формат(ВыборкаДетальныеЗаписи.ДатаОкончания, "ДФ=dd.MM.yyyy");
					Иначе
						СтрокаОпыта = СтрокаОпыта + " – по настоящее время";
					КонецЕсли;
					СтрокаОпыта = СтрокаОпыта + ", " + ВыборкаДетальныеЗаписи.ВидДеятельности;								
				КонецЦикла;
				ОбластьСтрока.Параметры.ПрактическийОпыт = СтрокаОпыта;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ТабДок.ПроверитьВывод(ОбластьСтрока) Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ТабДок.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СведенияОРаботникахЛаборатории", "Сведения о работниках лаборатории",
				СформироватьТабДокСведенийОСотрудниках(МассивОбъектов, ОбъектыПечати), , "Справочник.Сотрудники.СведенияОРаботникахЛаборатории");
	
	
КонецПроцедуры
