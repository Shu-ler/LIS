#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// заполним некоторые параметры открытия формы для передачи их в переопределяемую форму выбора
	ФормаВыбораДляОткрытияПараметры = Новый Структура("РежимВыбора,ТекущаяСтрока");
	ЗаполнитьЗначенияСвойств(ФормаВыбораДляОткрытияПараметры, Параметры);
	ФормаВыбораДляОткрытияПараметры.РежимВыбора = Истина; // форма выбора всегда в режиме выбора
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	Если Параметры.Свойство("ОтборОбъектПлатыНВОС") Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПроизводственнаяПлощадка.Владелец", 
		Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно,,Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ТолькоИВ") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Параметры.ТолькоИВ = Истина Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	    ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Родитель");
	    ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.Заполнено;
	    ЭлементОтбора.Использование    = Истина;
		Заголовок = "Источники выделения";
		
		ПризнакМетана = Ложь;
		Параметры.Отбор.Свойство("МетанПоУчетуДУ", ПризнакМетана);
		Если ПризнакМетана = Истина Тогда
			Заголовок = "Источники выделения метана";
			Элементы.Список.Подсказка = "Отображаются только источники выделения с установленным признаком 'Учет объема газа по данным ДУ'";
			Элементы.Список.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСверху;
		КонецЕсли;
		
		Элементы.Список.ИзменятьСоставСтрок = Ложь;
		
	ИначеЕсли Параметры.ТолькоИВ = Ложь Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	    ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Родитель");
	    ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	    ЭлементОтбора.Использование    = Истина;
		
		Элементы.НомерИсточникаВыделения.Видимость = 0;
		Элементы.ВидИВ.Видимость = 0;
		
		Заголовок = "Источники загрязнения";
		
			
	КонецЕсли;
	
	Если Параметры.Свойство("СписокИсключенныхИЗА") = Истина Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	    ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Родитель");
	    ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.НеВСписке;
	    ЭлементОтбора.Использование		= Истина;
		ЭлементОтбора.ПравоеЗначение	= Параметры.СписокИсключенныхИЗА;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ПроизводственнаяПлощадка") = Истина Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	    ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("ПроизводственнаяПлощадка");
	    ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	    ЭлементОтбора.Использование		= Истина;
		ЭлементОтбора.ПравоеЗначение	= Параметры.ПроизводственнаяПлощадка;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ПереченьВидыИВ") = Истина Тогда
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	    ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("ВидИВ");
	    ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.ВСписке;
	    ЭлементОтбора.Использование		= Истина;
		ЭлементОтбора.ПравоеЗначение	= Параметры.ПереченьВидыИВ;
		
		Если Параметры.ПереченьВидыИВ.Количество() = 0 Тогда
			Элементы.Список.Подсказка = "По выбранной технологической операции не установлены виды ИВ." + Символы.ПС + "Выполните установку через документ 'Установка технологических операций видов источников выделения'.";
			Элементы.Список.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСверху;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// если нет отбора по производственной площадке, организации или виду источника, то стандартная форма выбора будет не удобна для использования, откроем форму списка (она с иерархией) --старт
	ОткрытьТекущуюФормуВыбора = Ложь;
	
	ИскомыеПоляОтбора = Новый Массив;
	ИскомыеПоляОтбора.Добавить(Новый ПолеКомпоновкиДанных("ПроизводственнаяПлощадка"));
	ИскомыеПоляОтбора.Добавить(Новый ПолеКомпоновкиДанных("ПроизводственнаяПлощадка.Владелец"));
	ИскомыеПоляОтбора.Добавить(Новый ПолеКомпоновкиДанных("ВидИВ"));
	
	Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		Если ИскомыеПоляОтбора.Найти(ЭлементОтбора.ЛевоеЗначение) <> Неопределено 
				И ЭлементОтбора.Использование Тогда
				
			ОткрытьТекущуюФормуВыбора = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла; 
	
	Если Не ОткрытьТекущуюФормуВыбора Тогда
		ОткрытьФорму("Справочник.ИсточникиЗагрязнения.ФормаСписка",ФормаВыбораДляОткрытияПараметры,ВладелецФормы,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца );
		Закрыть(); // текущую форму выбора закрываем
	КонецЕсли;
	// если нет отбора по производственной площадке, организации или виду источника, то стандартная форма выбора будет не удобна для использования, откроем форму списка (она с иерархией) --финиш
	
КонецПроцедуры

#КонецОбласти
