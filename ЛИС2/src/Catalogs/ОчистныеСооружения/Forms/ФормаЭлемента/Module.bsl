
#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ДополнительныеОтчетыИОбработки
	
	Если Параметры.Ключ.Пустая() Тогда	
		ЗаполнитьОписания();
	КонецЕсли;//Если Параметры.Ключ.Пустая() Тогда
	
КонецПроцедуры // Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьОписания();
	
КонецПроцедуры // Процедура ПриЧтенииНаСервере(ТекущийОбъект)

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОписания()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОбЭксплуатацииСрезПоследних.ВведноВЭксплуатацию,
	|	СведенияОбЭксплуатацииСрезПоследних.Период КАК Период
	|ИЗ
	|	РегистрСведений.СведенияОбЭксплуатации.СрезПоследних(
	|			,
	|			Организация = &Организация
	|				И ЭксплуатируемыйОбъект = &ОчистноеСооружение) КАК СведенияОбЭксплуатацииСрезПоследних";
	
	Запрос.УстановитьПараметр("Организация", 		Объект.Владелец);
	Запрос.УстановитьПараметр("ОчистноеСооружение", Объект.Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		
		Выборка = Результат.Выбрать();
		
		Выборка.Следующий();
		
		Если Выборка.ВведноВЭксплуатацию Тогда 
			
			Элементы.СведенияОВводеВЭксплуатацию.Заголовок  = "Эксплуатируется с " + Формат(Выборка.Период, "ДФ='dd.MM.yyyy ""г.""'");
			Элементы.СведенияОВводеВЭксплуатацию.ЦветТекста = ЦветаСтиля.ЦветТекстаКонтрагентаДействующего;
			
		Иначе
			
			Элементы.СведенияОВводеВЭксплуатацию.Заголовок  = "Выведено из эксплуатации с " + Формат(Выборка.Период, "ДФ='dd.MM.yyyy ""г.""'");
			Элементы.СведенияОВводеВЭксплуатацию.ЦветТекста = ЦветаСтиля.ЦветТекстаНекорректногоКонтрагента;
			
		КонецЕсли; // Если Выборка.ВведноВЭксплуатацию Тогда
		
	КонецЕсли; // Если Не Результат.Пустой() Тогда 
		
КонецПроцедуры //Процедура ЗаполнитьОписания() 

#КонецОбласти
