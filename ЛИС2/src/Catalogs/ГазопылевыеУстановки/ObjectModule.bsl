Перем ПредыдущийВладелец; // предыдущий владелец этого элемента

#Область ОбработчикиСобытий

Процедура ПриЗаписи( Отказ )
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;		

	// Работа с бизнес-процессами
	Если Обработки.СозданиеНСИ.ИспользоватьМеханизмБизнесПроцессов( Ссылка ) Тогда
		Обработки.СозданиеНСИ.ЗапуститьБизнесПроцессСогласования( Ссылка );
	КонецЕсли;
	
	ОбновитьГПУвИЗА(ЭтотОбъект);

КонецПроцедуры

Процедура ПередУдалением( Отказ )

// 1СКСУ - Казанцев И.В. 19.01.17 11:15
// При провере АПК не обнаружена конструкция "Если ОбменДанными.Загрузка Тогда ...".
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 
// 1СКСУ - Казанцев И.В. 19.01.17 11:15 Конец

	
	// Работа с бизнес-процессами
	Если Обработки.СозданиеНСИ.ИспользоватьМеханизмБизнесПроцессов( Ссылка ) Тогда
		Если Не Обработки.СозданиеНСИ.ЭлементДоступенДляРедактирования( Ссылка ) тогда
			Отказ = истина;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// записывает ГПУ в реквизит ИЗА
//	если в ГПУ изменился владелец (ИЗА), то у старого ИЗА ГПУ стираем
//
// Параметры:
//  ГПУ  - СправочникОбъект.ГазопылевыеУстановки - записываемый ГПУ
//
Процедура ОбновитьГПУвИЗА(ГПУ)
	
	ИЗАНовыйСсылка = ГПУ.Владелец;
	
	// обновим старый ИЗА
	Если ЗначениеЗаполнено(ПредыдущийВладелец) И (ПредыдущийВладелец <> ИЗАНовыйСсылка) Тогда // если в ГПУ поменяли владельца, у старого владельца очищаем реквизит
		ИЗАСтарыйОбъект = ПредыдущийВладелец.ПолучитьОбъект();
		ИЗАСтарыйОбъект.ГПУ = Неопределено;
		ИЗАСтарыйОбъект.Записать();
	КонецЕсли; 
	
	// обновим новый ИЗА
	ИЗАНовыйОбъект = ИЗАНовыйСсылка.ПолучитьОбъект();
	Если ИЗАНовыйОбъект.ГПУ <> ГПУ.Ссылка Тогда // Если у ИЗА ГПУ не равен текущему, обновляем
		ИЗАНовыйОбъект.ГПУ = ГПУ.Ссылка;
		ИЗАНовыйОбъект.Записать();	
	КонецЕсли; 
	
КонецПроцедуры // ОбновитьГПУвИЗА()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	// запомним владельца перед записью, для обнаружения изменения владельца
	Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда // если объект записан
		ПредыдущийВладелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Ссылка,"Владелец");
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
