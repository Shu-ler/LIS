
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗначенияКоэффициентовСрезПоследних.Период,
			|	ЗначенияКоэффициентовСрезПоследних.Значение
			|ИЗ
			|	РегистрСведений.ЗначенияКоэффициентов.СрезПоследних(, Коэффициент = &Коэффициент) КАК ЗначенияКоэффициентовСрезПоследних";
		
		Запрос.УстановитьПараметр("Коэффициент", Объект.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда

			ДатаТекущегоЗначения = ВыборкаДетальныеЗаписи.Период;
			ТекущееЗначение = ВыборкаДетальныеЗаписи.Значение;
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ЗаписьРегистраКоэффициентов = РегистрыСведений.ЗначенияКоэффициентов.СоздатьМенеджерЗаписи();
	ЗаписьРегистраКоэффициентов.Период 		= ДатаНовогоЗначения;
	ЗаписьРегистраКоэффициентов.Коэффициент = ТекущийОбъект.Ссылка;
	ЗаписьРегистраКоэффициентов.Значение 	= ТекущийОбъект.Значение;
	ЗаписьРегистраКоэффициентов.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзменениеЗначенияКоэффициента"); 
	
КонецПроцедуры

#КонецОбласти

