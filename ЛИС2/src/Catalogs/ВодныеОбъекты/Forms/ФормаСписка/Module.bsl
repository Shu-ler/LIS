#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ОбщегоНазначения.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВодохозяйственныеУчасткиПриАктивизацииСтроки(Элемент)
	
	ВодохозяйственныйУчастокОтбора = ОпределитьТекущийВодохозяйственныйУчастокСписка();
	
	Если ВодохозяйственныйУчастокОтбора = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВХУ", ВодохозяйственныйУчастокОтбора, , , Ложь);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВХУ", ВодохозяйственныйУчастокОтбора, ВидСравненияКомпоновкиДанных.ВСписке, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ОпределитьТекущийВодохозяйственныйУчастокСписка()
	
	ВодохозяйственныйУчастокОтбора = Неопределено;
	
	ТекущиеДанные = Элементы.ВодохозяйственныеУчастки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда 
		
		ВодохозяйственныйУчастокОтбора = ПолучитьСписокВХУ(ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
	Возврат ВодохозяйственныйУчастокОтбора;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокВХУ(ВодохозяйственныеУчасткиОтбора)
	
	Если ВодохозяйственныеУчасткиОтбора.ЭтоГруппа Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВодохозяйственныеУчастки.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ВодохозяйственныеУчастки КАК ВодохозяйственныеУчастки
			|ГДЕ
			|	НЕ ВодохозяйственныеУчастки.ЭтоГруппа
			|	И ВодохозяйственныеУчастки.Родитель В ИЕРАРХИИ(&Родитель)";
		
		Запрос.УстановитьПараметр("Родитель", ВодохозяйственныеУчасткиОтбора);
		
		ВодохозяйственныйУчастокОтбора = Новый СписокЗначений();
		ВодохозяйственныйУчастокОтбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	Иначе
		ВодохозяйственныйУчастокОтбора = Новый СписокЗначений();
		ВодохозяйственныйУчастокОтбора.Вставить(0, ВодохозяйственныеУчасткиОтбора);
	КонецЕсли;
	
	Возврат ВодохозяйственныйУчастокОтбора;
	
КонецФункции;
#КонецОбласти
