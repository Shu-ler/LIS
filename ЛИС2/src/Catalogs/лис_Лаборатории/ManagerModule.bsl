
Функция СформироватьТабДокНормативныхДокументов(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Справочники.лис_Лаборатории.ПолучитьМакет("ПереченьНД");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	лис_ЛабораторииПереченьМетодикИзмерения.НомерСтроки КАК Номер,
	|	лис_ЛабораторииПереченьМетодикИзмерения.РегистрационныйНомер,
	|	лис_ЛабораторииПереченьМетодикИзмерения.МетодикаИзмерения.ПолноеНаименование КАК НаименованиеДокумента,
	|	лис_ЛабораторииПереченьМетодикИзмерения.МетодикаИзмерения.Представление КАК МетодикаКраткое
	|ИЗ
	|	Справочник.лис_Лаборатории.ПереченьМетодикИзмерения КАК лис_ЛабораторииПереченьМетодикИзмерения
	|ГДЕ
	|	лис_ЛабораторииПереченьМетодикИзмерения.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов[0]);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		
		Если НЕ ТабДок.ПроверитьВывод(ОбластьСтрока) Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	Возврат ТабДок;
		
КонецФункции

Функция СформироватьТабДокПеречняСИ(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Лаборатория = МассивОбъектов[0];
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЛабораторноеОборудованиеВЭксплуатации.Оборудование,
	|	ЛабораторноеОборудованиеВЭксплуатации.РегистрационныйНомер КАК РегНомер,
	|	ЛабораторноеОборудованиеВЭксплуатации.ИнвентарныйНомер,
	|	ЛабораторноеОборудованиеВЭксплуатации.ЗаводскойНомер,
	|	ЛабораторноеОборудованиеВЭксплуатации.Оборудование.Наименование КАК НаименованиеСИ,
	|	ЛабораторноеОборудованиеВЭксплуатации.ДатаВводаВЭксплуатацию КАК ГодВводаВЭксплуатацию,
	|	ЛабораторноеОборудованиеВЭксплуатации.Оборудование.Изготовитель КАК Организация,
	|	ЛабораторноеОборудованиеВЭксплуатации.ДатаИзготовления КАК ГодВыпуска,
	|	ЛабораторноеОборудованиеВЭксплуатации.Оборудование.Тип КАК Тип,
	|	ЛабораторноеОборудованиеВЭксплуатации.Оборудование.Изготовитель.Страна КАК Страна,
	|	ЛабораторноеОборудованиеВЭксплуатации.Оборудование.КлассТочности КАК КлассТочности,
	|	ЛабораторноеОборудованиеВЭксплуатации.Лаборатория КАК МестоХранения,
	|	ЛабораторноеОборудованиеВЭксплуатации.Оборудование.Погрешность КАК Погрешность
	|ИЗ
	|	РегистрСведений.ЛабораторноеОборудованиеВЭксплуатации КАК ЛабораторноеОборудованиеВЭксплуатации
	|ГДЕ
	|	ЛабораторноеОборудованиеВЭксплуатации.Организация = &Организация
	|	И ЛабораторноеОборудованиеВЭксплуатации.Лаборатория = &Лаборатория";
	
	Запрос.УстановитьПараметр("Организация", Лаборатория.Владелец);
	Запрос.УстановитьПараметр("Лаборатория", Лаборатория);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Макет = Справочники.лис_Лаборатории.ПолучитьМакет("СведенияОбОснащенностиСредствамиИзмерений");
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДок.Вывести(ОбластьМакета);
	
	НомерСтроки = 1;
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.Номер = НомерСтроки;
		
		ТабДок.Вывести(ОбластьМакета);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;	
	
	Возврат ТабДок;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПереченьНД") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПереченьНД", "Перечень нормативных документов",
					СформироватьТабДокНормативныхДокументов(МассивОбъектов, ОбъектыПечати), , "Справочник.лис_Лаборатории.ПереченьНД");
					
	Иначе
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СведенияОбОснащенностиСредствамиИзмерений", "Сведения об оснащенности средствами измерений",
					СформироватьТабДокПеречняСИ(МассивОбъектов, ОбъектыПечати), , "Справочник.лис_Лаборатории.СведенияОбОснащенностиСредствамиИзмерений");
		
	КонецЕсли;
	
	
	
КонецПроцедуры
