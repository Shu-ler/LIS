#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	ВидПротокола = Неопределено;
	Параметры.Свойство("ВидПротокола", ВидПротокола);
	
	Если ВидПротокола = Перечисления.ВидыПротоколаПромышленныхВыбросов.ДляРасчета Тогда
		
		СписокЗВ = Новый СписокЗначений;
		СписокЗВ.Добавить(Справочники.ЗВАтмосфера.ОксидУглерода.Ссылка);
		СписокЗВ.Добавить(Справочники.ЗВАтмосфера.ОксидАзота.Ссылка);
		СписокЗВ.Добавить(Справочники.ЗВАтмосфера.ДиоксидАзота.Ссылка);
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	    ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Ссылка");
	    ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.ВСписке;
	    ЭлементОтбора.Использование    = Истина;
	    ЭлементОтбора.ПравоеЗначение   = СписокЗВ;
		
		Элементы.ПереключательРазрешения.Видимость = Ложь;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ДействующееРазрешение) = Истина Тогда
		
		ДанныеРазрешения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ДействующееРазрешение, "РегистрационныйНомер, ДатаВыдачи, ТаблицаЗВ");
		
		ТаблицаЗВРазрешения = ДанныеРазрешения.ТаблицаЗВ.Выгрузить();
		ПереченьЗВРазрешения.ЗагрузитьЗначения(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗВРазрешения,"ЗагрязняющееВещество"));
		
		СтрокаРазрешения = "№ " + ДанныеРазрешения.РегистрационныйНомер + " от " + Формат(ДанныеРазрешения.ДатаВыдачи,"ДФ=dd.MM.yyyy");
		
	Иначе 
		
		Элементы.ПереключательРазрешения.Видимость = Ложь;
		
		СтрокаРазрешения = "Выброс без документа разрешения";
		
	КонецЕсли;
	
	ПереключательРазрешения = "Полный";

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура ПереключательРазрешенияПриИзменении(Элемент)
	
	Если ПереключательРазрешения = "Полный" Тогда
	
		Список.Отбор.Элементы.Очистить();
		
	ИначеЕсли ПереключательРазрешения = "ИзДокументаРазрешения" Тогда

		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование    = Истина;
		ЭлементОтбора.ПравоеЗначение   = ПереченьЗВРазрешения;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
